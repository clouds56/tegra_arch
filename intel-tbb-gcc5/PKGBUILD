# $Id$
# Maintainer:
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Denis Martinez <deuns.martinez AT gmail.com>

pkgname=intel-tbb-gcc5
pkgver=2018_20171205
_pkgver=2018_U2
pkgrel=1
pkgdesc='High level abstract threading library'
arch=(aarch64)
url='http://www.threadingbuildingblocks.org/'
license=(GPL)
depends=(gcc-libs gcc5)
source=(https://github.com/01org/tbb/archive/$_pkgver.tar.gz)
sha256sums=('78bb9bae474736d213342f01fe1a6d00c6939d5c75b367e2e43e7bf29a6d8eca')

build() {
  cd tbb-$_pkgver

  # This option exist in default makepkg.conf while it's not supported by gcc5
  CFLAGS=${CFLAGS/-fno-plt/}
  CXXFLAGS=${CXXFLAGS/-fno-plt/}

  sed -i "s#CONLY\s*=\s*gcc#\0\nCC ?= gcc#" build/linux.gcc.inc
  sed -i 's#\(shell\s*\)gcc#\1 $(CC)#' build/{linux.inc,linux.gcc.inc}

  [[ $CARCH == "armv7h" ]] && CXXFLAGS+=" -DTBB_USE_GCC_BUILTINS=1 -D__TBB_64BIT_ATOMICS=0"
  [[ $CARCH == "aarch64" ]] && CXXFLAGS+=" -DTBB_USE_GCC_BUILTINS=1 -D__TBB_64BIT_ATOMICS=1"

  make CC=gcc-5 CXX=g++-5
}

package() {
  cd tbb-$_pkgver
  install -d "$pkgdir"/usr/lib/gcc/aarch64-unknown-linux-gnu/5.5.0
  install -m755 build/linux_*/*.so* "$pkgdir"/usr/lib/gcc/aarch64-unknown-linux-gnu/5.5.0
  install -d "$pkgdir"/usr/lib/gcc/aarch64-unknown-linux-gnu/5.5.0/include
  cp -a include/tbb "$pkgdir"/usr/lib/gcc/aarch64-unknown-linux-gnu/5.5.0/include
}
